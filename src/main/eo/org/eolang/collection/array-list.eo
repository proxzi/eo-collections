+package org.eolang.collection
+rt jvm org.eolang:eo-collection:0.0.0

[arr] > array-list

  [el] > contains
    greater. > @
      index-of el
      -1

  # return -1 if index out of range
  [el] > index-of

    rec-index-of > @
      arr
      el
      0

    [arr el i] > rec-index-of
      if. > @
        eq.
          arr.length
          i
        -1
        if.
          eq.
            el
            (arr.get i)
          i
          rec-index-of
            arr
            el
            (i.add 1)

  [el] > add
    array-list > @
      append.
        arr
        el

  [in-arr] > add-all
    array-list > @
      reduce.
        in-arr.arr
        arr
        [res cur]
          append. > @
            res
            cur

  [i el] > insert

    array-list > @
      reduce.
        end-arr
        append.
          begin-arr
          el
        [res cur]
          append. > @
            res
            cur

    rec-insert > begin-arr
      arr
      *
      0
      i

    rec-insert > end-arr
      arr
      *
      i
      (arr.length)

    [in-arr accum start end] > rec-insert
      if. > @
        less.
          start
          end
        rec-insert
          in-arr
          append.
            accum
            get.
              in-arr
              start
          add.
            start
            1
          end
        accum

  [i] > get
    arr.get i > @

  [in-arr] > eq
    equalarrs > @
      in-arr.arr
      arr

    [x y] > equalarrs
      y > copy
      if. > @
        eq.
          copy.length
          x.length
        get.
          copy.mapi
            [e i]
              [idx] > cond
                eq. > @
                  copy.get idx
                  x.get idx
              if. > @
                i.less 1
                cond i
                cond
                  i.sub 1
          copy.length.sub 1
        FALSE

  [from-idx to-idx] > sub-list

    array-list > @
      rec-sub-list
        arr
        *
        from-idx
        to-idx

    [in-arr accum start end] > rec-sub-list
      if. > @
        less.
          start
          end
        rec-sub-list
          in-arr
          append.
            accum
            get.
              in-arr
              start
          add.
            start
            1
          end
        accum

  [i] > remove

    array-list > @
      reduce.
        (end-arr.arr)
        (begin-arr.arr)
        [res cur]
          append. > @
            res
            cur

    sub-list > begin-arr
      0
      i

    sub-list > end-arr
      (i.add 1)
      (arr.length)

